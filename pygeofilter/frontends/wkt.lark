// ------------------------------------------------------------------------------
//
// Project: pygeofilter <https://github.com/geopython/pygeofilter>
// Authors: Fabian Schindler <fabian.schindler@eox.at>
//
// ------------------------------------------------------------------------------
// Copyright (C) 2021 EOX IT Services GmbH
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies of this Software or works derived from this Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
// ------------------------------------------------------------------------------

?ewkt_geometry: "SRID" "=" INT ";" geometry                             -> geometry_with_srid
              | geometry

?geometry: point
         | linestring
         | polygon
         | multipoint
         | multilinestring
         | multipolygon
         | geometrycollection

geometrycollection: "GEOMETRYCOLLECTION" "(" geometry ( "," geometry )* ")"
point: "POINT" "(" coordinate ")"
linestring: "LINESTRING" "(" coordinate_list ")"
polygon: "POLYGON" "(" coordinate_lists ")"

multipoint: "MULTIPOINT" "(" coordinate_list ")"                                -> multipoint
          | "MULTIPOINT" "(" "(" coordinate ")" ( "," "(" coordinate ")" )* ")" -> multipoint_2
multilinestring: "MULTILINESTRING" "(" coordinate_lists ")"

multipolygon: "MULTIPOLYGON" "(" "(" coordinate_lists ")" ( "," "(" coordinate_lists ")" )* ")"

coordinate_lists: "(" coordinate_list ")" ( "," "(" coordinate_list ")" )*

?coordinate_list: coordinate_list "," coordinate
                | coordinate                                            -> coordinate_list_start
coordinate: SIGNED_NUMBER SIGNED_NUMBER [ SIGNED_NUMBER [ SIGNED_NUMBER ] ]

// NUMBER: /-?\d+\.?\d+/
%import common.NUMBER
%import common.SIGNED_NUMBER
%import common.INT
